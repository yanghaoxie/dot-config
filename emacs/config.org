#+Title: Emacs Configuration
#+Author: Meiyo Peng
#+Email: meiyo@riseup.net
#+Date: <2019-03-18 Mon>
#+Copyright: Copyright 2015-2019 Meiyo Peng
#+License: GPLv3+

#+PROPERTY: header-args+ :results silent
#+PROPERTY: header-args+ :eval no-export
#+PROPERTY: header-args+ :comments org
#+PROPERTY: header-args:emacs-lisp :tangle ~/.emacs.d/init.el


#+BEGIN_VERSE
  Join the [[http://www.stallman.org/saint.html][Church of Emacs]].
  Hold the holy ~M-x butterfly~.
  Unleash the powers of the butterfly.
#+END_VERSE

#+BEGIN_QUOTE
  Emacs saves you time when you work, and takes it back when you play with it.
#+END_QUOTE

* init.el
  Use ~M-x org-babel-tangle~ or ~C-c C-v t~ to manually extract lisp code.

* Personal Information
  name and mail address
  #+BEGIN_SRC emacs-lisp
    (setq user-full-name "Meiyo Peng"
          user-mail-address "meiyo@riseup.net")
  #+END_SRC

  Id
  #+BEGIN_SRC emacs-lisp
    (setq my-gmail-address "meiyo.peng@gmail.com")
    (setq my-irc-nick "meiyopeng")
  #+END_SRC

* Bootstrap
** Enforce minimum Emacs version
   #+BEGIN_SRC emacs-lisp
     (let ((min-version "26.0.50"))
       (when (version< emacs-version min-version)
         (error "Gnu Emacs %s or newer is required" min-version)))
   #+END_SRC

   #+BEGIN_SRC emacs-lisp
     (setq load-prefer-newer t)
   #+END_SRC

** Emacs server
   Detect existing emacs server and switch to it if possible
   #+BEGIN_SRC emacs-lisp
     (require 'server)

     (defun my-server-shunt ()
       "Shunts to emacsclient"
       (let ((args (append '("emacsclient" "-a" "\"\"" "-n")
                           (cdr command-line-args))))
         (shell-command (substring (format "%S" args) 1 -1))
         (kill-emacs)))

     ;; Keep only one Emacs server instance
     (if (server-running-p)
         (if (daemonp)
             (error "Another running emacs server detected, abort")
           (my-server-shunt))
       (server-start))
   #+END_SRC

** XDG specification
   Define XDG directories
   #+BEGIN_SRC emacs-lisp
     (require 'xdg)

     (defvar user-emacs-config-dir
       (expand-file-name "emacs" (xdg-config-home)))

     (defvar user-emacs-data-dir
       (expand-file-name "emacs" (xdg-data-home)))

     (defvar user-emacs-cache-dir
       (expand-file-name "emacs" (xdg-cache-home)))
   #+END_SRC

   user-emacs-directory
   #+BEGIN_SRC emacs-lisp :tangle no
     (setq user-emacs-directory (concat user-emacs-data-dir "/"))
   #+END_SRC

** Package manager
   #+BEGIN_SRC emacs-lisp
     (require 'package)
     (setq package-archives '(("gnu" . "https://elpa.gnu.org/packages/")))
     (add-to-list 'package-archives '("melpa" . "https://melpa.org/packages/"))
     (add-to-list 'package-archives '("org" . "https://orgmode.org/elpa/"))
     (package-initialize)
   #+END_SRC

** Bootstrap use-package
   #+BEGIN_SRC emacs-lisp
     (unless (package-installed-p 'use-package)
       (package-refresh-contents)
       (package-install 'use-package))

     (eval-when-compile
       (require 'use-package))
     (require 'bind-key)

     (setq use-package-verbose t)

     ;; Disable lazy loading in daemon mode
     (if (daemonp)
         (setq use-package-always-demand t))
   #+END_SRC

** custom.el
   Variables configured via the interactive 'customize' interface
   #+BEGIN_SRC emacs-lisp
     (setq custom-file (expand-file-name "custom.el" user-emacs-config-dir))
     (load custom-file 'noerror)
   #+END_SRC

* Core
  Emacs built-in features

** environment
*** Determine operating system type
    #+BEGIN_SRC emacs-lisp
      (defconst *os-is-gnu* (eq system-type 'gnu/linux))
      (defconst *os-is-mac* (eq system-type 'darwin))
      (defconst *os-is-windows* (eq system-type 'windows-nt))
    #+END_SRC

*** Language
    #+BEGIN_SRC emacs-lisp
      (set-language-environment "UTF-8")
    #+END_SRC

*** Locale
    set-locale-environment changes the default coding system, so it should be
    called before setting coding system.
    #+BEGIN_SRC emacs-lisp
      (if *os-is-gnu*
          (set-locale-environment "en_US.UTF-8"))
      (if *os-is-mac*
          (set-locale-environment "en_US.UTF-8"))
      (if *os-is-windows*
          (set-locale-environment "ENU"))
    #+END_SRC

*** Encoding
    #+BEGIN_SRC emacs-lisp
      ;;(set-default-coding-systems 'utf-8-unix)
      (prefer-coding-system 'utf-8-unix)
    #+END_SRC

    Use hexadecimal instead of octal for quoted-insert (C-q).
    #+BEGIN_SRC emacs-lisp
      (setq read-quoted-char-radix 16)
    #+END_SRC

*** Font
    #+BEGIN_SRC emacs-lisp
      (defun my-font-available-p (font)
        "Detect if a font is available"
        (if (find-font (font-spec :family font))
            t
          nil))

      (require 'cl)

      (defun my-set-font (font-list &optional font-size)
        "Set default font to the first available font in FONT-LIST"
        (let ((font (cl-find-if #'my-font-available-p font-list)))

          (if (null font)
              (user-error "No font is available in FONT-LIST"))

          (message "Set default font to %s" font)
          (set-face-font 'default
                         (font-spec :family font :size font-size))))
    #+END_SRC

    My preferred fonts
    #+BEGIN_SRC emacs-lisp
      (when (display-graphic-p)
        (my-set-font '("DejaVu Sans Mono" "Menlo" "Consolas" "Monospace")))
    #+END_SRC

** basic interface
   =*scratch*= buffer's default content
   #+BEGIN_SRC emacs-lisp
     (setq initial-scratch-message nil)
   #+END_SRC

   hide all kinds of bars
   #+BEGIN_SRC emacs-lisp
     (menu-bar-mode -1)
     (if (fboundp 'tool-bar-mode)
         (tool-bar-mode -1))
     (if (fboundp 'scroll-bar-mode)
         (scroll-bar-mode -1))
   #+END_SRC

   mode line
   #+BEGIN_SRC emacs-lisp
     (line-number-mode t)
     (column-number-mode t)

     (size-indication-mode t)
   #+END_SRC

   ring
   #+BEGIN_SRC emacs-lisp
     (setq ring-bell-function 'ignore)
   #+END_SRC

   buffer name
   #+BEGIN_SRC emacs-lisp
     (require 'uniquify)
     (setq uniquify-buffer-name-style 'forward)
     (setq uniquify-separator "/")
     (setq uniquify-after-kill-buffer-p t)
     (setq uniquify-ignore-buffers-re "^\\*")
   #+END_SRC

   frame name
   #+BEGIN_SRC emacs-lisp
     ;; show either a file or a buffer name
     (setq frame-title-format
           '("" invocation-name " - "
             (:eval (if (buffer-file-name)
                        (abbreviate-file-name (buffer-file-name))
                      "%b"))))
   #+END_SRC

** key bindings
   yes-or-no-p
   #+BEGIN_SRC emacs-lisp
     (defalias 'yes-or-no-p 'y-or-n-p)
   #+END_SRC

   bind ~C-x k~ to ~kill-this-buffer~
   #+BEGIN_SRC emacs-lisp
     (global-set-key (kbd "C-x k") 'kill-this-buffer)
   #+END_SRC

   expand text
   #+BEGIN_SRC emacs-lisp
     (global-set-key (kbd "M-/") 'hippie-expand)
   #+END_SRC

   upcase or downcase text
   #+BEGIN_SRC emacs-lisp
     (global-set-key (kbd "M-u") 'upcase-dwim)
     (global-set-key (kbd "M-l") 'downcase-dwim)
   #+END_SRC

   shell
   #+BEGIN_SRC emacs-lisp
     (global-set-key (kbd "C-c s") 'eshell)
   #+END_SRC

** editing
   fill column
   #+BEGIN_SRC emacs-lisp
     (setq-default fill-column 80)
   #+END_SRC

   final new line
   #+BEGIN_SRC emacs-lisp
     (setq require-final-newline t)
   #+END_SRC

   delete the selection with a key press
   #+BEGIN_SRC emacs-lisp
     (delete-selection-mode t)
   #+END_SRC

   smart tab key behavior, indent or complete
   #+BEGIN_SRC emacs-lisp
     (setq tab-always-indent 'complete)
   #+END_SRC

   indentation
   #+BEGIN_SRC emacs-lisp
     ;; don't use tabs to indent
     (setq-default indent-tabs-mode nil)

     (setq-default tab-width 8)
   #+END_SRC

   Revert buffers automatically when underlying files are changed externally
   #+BEGIN_SRC emacs-lisp
     (global-auto-revert-mode t)
   #+END_SRC

   Automatically save buffers to file when losing focus
   #+BEGIN_SRC emacs-lisp
     (defun my-save-buffers ()
       "Save all file-visiting buffers"
       (save-some-buffers t nil))

     (add-hook 'focus-out-hook 'my-save-buffers)
   #+END_SRC

   Automatically make a shell script executable on save
   #+BEGIN_SRC emacs-lisp
     (add-hook 'after-save-hook
               'executable-make-buffer-file-executable-if-script-p)
   #+END_SRC

** highlight
   #+BEGIN_SRC emacs-lisp
     (blink-cursor-mode -1)

     ;; highlight the current line
     (global-hl-line-mode 1)

     ;; highlight matching parentheses when the point is on them
     (show-paren-mode t)

     (setq blink-matching-paren nil)
   #+END_SRC

   whitespace-mode
   #+BEGIN_SRC emacs-lisp
     (require 'whitespace)
     (setq whitespace-style '(face empty trailing lines-tail indentation))
     (setq whitespace-line-column 80)

     (defun my-whitespace-mode-setup ()
       (whitespace-mode 1)
       (add-hook 'before-save-hook 'whitespace-cleanup nil t))
   #+END_SRC

** basic major modes
*** text-mode
    #+BEGIN_SRC emacs-lisp
      (add-hook 'text-mode-hook 'auto-fill-mode)
      (add-hook 'text-mode-hook 'my-whitespace-mode-setup)
    #+END_SRC

*** prog-mode
    #+BEGIN_SRC emacs-lisp
      (add-hook 'prog-mode-hook 'abbrev-mode)
      (add-hook 'prog-mode-hook 'my-whitespace-mode-setup)

      (defun my-prog-mode-setup ()
        (which-function-mode 1)

        (setq-local comment-auto-fill-only-comments t)
        (auto-fill-mode 1)

        ;; highlight a bunch of well known comment annotations
        (font-lock-add-keywords
         nil
         '(("\\<\\(\\(FIX\\(ME\\)?\\|TODO\\|OPTIMIZE\\|HACK\\|REFACTOR\\):\\)"
            1 font-lock-warning-face t))))

      (add-hook 'prog-mode-hook 'my-prog-mode-setup)
    #+END_SRC

** tramp
   #+BEGIN_SRC emacs-lisp
     (require 'tramp)
     (setq tramp-default-method "ssh")
   #+END_SRC

** dired
   #+BEGIN_SRC emacs-lisp
     (setq dired-recursive-copies 'always)
     (setq dired-recursive-deletes 'always)
     (require 'dired-x)
   #+END_SRC

** bookmark
   #+BEGIN_SRC emacs-lisp
     (require 'bookmark)
     (setq bookmark-save-flag 1)
   #+END_SRC

** internet
   Donâ€™t send anything in HTTP header field
   #+BEGIN_SRC emacs-lisp
     (setq url-privacy-level 'paranoid)
   #+END_SRC

*** Proxy
    SOCKS 5 proxy
    #+BEGIN_SRC emacs-lisp :tangle no
      (setq url-gateway-method 'socks)
      (setq socks-server '("Default server" "localhost" 1080 5))
    #+END_SRC

    HTTP proxy
    #+BEGIN_SRC emacs-lisp :tangle no
      (setq url-proxy-services
            '(("no_proxy" . "^\\(localhost\\|10\\..*\\|192\\.168\\..*\\)")
              ("http" . "localhost:1081")
              ("https" . "localhost:1081")))
    #+END_SRC

*** Browser
    eww
    #+BEGIN_SRC emacs-lisp
      (global-set-key (kbd "C-c w") 'eww)
      (global-set-key (kbd "C-c b") 'eww-list-bookmarks)
    #+END_SRC

*** Email
    message mode
    #+BEGIN_SRC emacs-lisp
      ;; Turn on PGP
      (add-hook 'message-mode-hook 'epa-mail-mode)

      ;; Message signature
      (setq message-signature-directory
            (expand-file-name "signature" (xdg-config-home)))
      (setq message-signature-file "personal")

      ;; Don't keep message buffer after sending a message.
      (setq message-kill-buffer-on-exit t)
    #+END_SRC

    SMTP
    #+BEGIN_SRC emacs-lisp :tangle no
      (setq message-send-mail-function 'message-smtpmail-send-it)

      (setq smtpmail-smtp-server "smtp.mailgun.com"
            smtpmail-stream-type 'ssl  ;; StartTLS is evil.
            smtpmail-smtp-service 465)
    #+END_SRC

    sendmail
    #+begin_src emacs-lisp
      (setq message-send-mail-function 'message-send-mail-with-sendmail)

      ;; Use the "From:" address in mail header as envelope-from address.
      (setq mail-specify-envelope-from t
            mail-envelope-from 'header)
      (setq message-sendmail-envelope-from 'header)
    #+end_src

    msmtp
    #+begin_src emacs-lisp :tangle no
      (setq sendmail-program "msmtp")
    #+end_src

** security
*** GPG
    Query passphrase through the minibuffer, instead of the pinentry program
    #+BEGIN_SRC emacs-lisp :tangle no
      (setq epg-pinentry-mode 'loopback)
    #+END_SRC

*** auth-source
    #+BEGIN_SRC emacs-lisp
      (setq auth-sources
            (list (expand-file-name "auth/netrc.gpg" (xdg-data-home))))
    #+END_SRC

    Get secret from auth-source
    #+BEGIN_SRC emacs-lisp
      (defun my-get-secret (&rest spec &key domain port user &allow-other-keys)
        (let ((record (nth 0 (auth-source-search :max 1
                                                 :host domain
                                                 :port port
                                                 :user user
                                                 :require '(:secret)))))
          (if record
              (let ((secret (plist-get record :secret)))
                (if (functionp secret)
                    (funcall secret)
                  secret))
            nil)))
    #+END_SRC

** session
*** desktop
    #+BEGIN_SRC emacs-lisp
      (setq desktop-path (list user-emacs-data-dir)
            desktop-auto-save-timeout 600)
      (desktop-save-mode t)
    #+END_SRC

*** recent files
    #+BEGIN_SRC emacs-lisp
      (require 'recentf)
      (setq recentf-auto-cleanup 'never)
      (setq recentf-exclude '("/gnu" "/tmp" "/ssh:" "~/.cache"))
      (add-to-list 'recentf-exclude
                   (expand-file-name  "elpa/.*" user-emacs-directory))
      (recentf-mode 1)
    #+END_SRC

*** minibuffer history
    #+BEGIN_SRC emacs-lisp
      (savehist-mode 1)
    #+END_SRC

*** auto-save
    #+BEGIN_SRC emacs-lisp
      (setq auto-save-list-file-prefix
            (expand-file-name "auto-save-list/" user-emacs-cache-dir))
    #+END_SRC

*** backup
    #+BEGIN_SRC emacs-lisp
      (let ((backup-dir (expand-file-name "backup" user-emacs-cache-dir)))
        (setq-default backup-directory-alist `((".*" . ,backup-dir))))
    #+END_SRC

* Theme
** theme
   #+BEGIN_SRC emacs-lisp
     (use-package zenburn-theme
       :ensure t
       :config
       (load-theme 'zenburn))
   #+END_SRC

** transparency
   alpha '(<active> . <inactive>)
   #+BEGIN_SRC emacs-lisp :tangle no
     (set-frame-parameter (selected-frame) 'alpha '(95 . 60))
   #+END_SRC

   #+BEGIN_SRC emacs-lisp
     (add-to-list 'default-frame-alist '(alpha . (98 . 80)))
   #+END_SRC

** mode line
   #+BEGIN_SRC emacs-lisp
     (use-package diminish
       :ensure t
       :config
       (diminish 'abbrev-mode)
       (diminish 'auto-fill-function)
       (diminish 'auto-revert-mode)
       (diminish 'eldoc-mode)
       (diminish 'whitespace-mode))
   #+END_SRC

** cursor
   highlight the cursor whenever the window scrolls
   #+BEGIN_SRC emacs-lisp
     (use-package beacon
       :ensure t
       :diminish beacon-mode
       :config
       (beacon-mode t))
   #+END_SRC

* Utilities
** helm
   #+BEGIN_SRC emacs-lisp
     (use-package helm
       :ensure t
       :defer 3
       :diminish helm-mode
       :bind-keymap ("C-c h" . helm-command-map)
       :bind (("C-c f" . helm-recentf)
              ("C-h a" . helm-apropos)
              ("C-x b" . helm-mini)
              ("C-x C-b" . helm-buffers-list)
              ("C-x C-d" . helm-browse-project)
              ("C-x C-f" . helm-find-files)
              ("M-x" . helm-M-x)
              ("M-y" . helm-show-kill-ring)
              ("M-s o" . helm-occur)
              :map helm-command-map
              ("g" . helm-do-grep-rg)
              ("M-g g" . helm-do-grep-rg))
       :init
       (defalias 'helm-do-grep-rg 'helm-do-grep-ag)
       :config
       (require 'helm-config)
       (helm-mode 1)

       (setq helm-move-to-line-cycle-in-source t)

       ;; fuzzy matching
       (setq helm-mode-fuzzy-match t)
       (setq helm-completion-in-region-fuzzy-match t)
       (setq helm-M-x-fuzzy-match t
             helm-buffers-fuzzy-matching t
             helm-recentf-fuzzy-match t)

       (add-to-list 'helm-mini-default-sources 'helm-source-bookmarks 'append)

       (setq helm-ff-file-name-history-use-recentf t)
       (setq helm-ff-skip-boring-files t)

       ;; ripgrep
       (setq helm-grep-ag-command "rg --color=always --colors 'match:fg:black' --colors 'match:bg:yellow' --smart-case --no-heading --line-number %s %s %s")
       (setq helm-grep-ag-pipe-cmd-switches '("--colors 'match:fg:black'" "--colors 'match:bg:yellow'"))
       )
   #+END_SRC

   helm-ls-git
   #+BEGIN_SRC emacs-lisp
     (use-package helm-ls-git
       :ensure t
       :after helm)
   #+END_SRC

** projectile
   #+BEGIN_SRC emacs-lisp
     (use-package projectile
       :ensure t
       :defer 10
       :diminish projectile-mode
       :bind-keymap (("C-c p" . projectile-command-map)
                     ("s-p" . projectile-command-map))
       :config
       (projectile-global-mode t)
       (setq projectile-project-search-path '("~/Projects"))

       ;; Search method
       (setq projectile-indexing-method 'alien)
       (setq projectile-generic-command "fd . -0")
       (setq projectile-git-command "fd . -0"))

     (use-package helm-projectile
       :ensure t
       :after (helm projectile)
       :config
       (helm-projectile-on))
   #+END_SRC

** file explorer
   #+BEGIN_SRC emacs-lisp
     (use-package dired-sidebar
       :ensure t
       :commands (dired-sidebar-toggle-sidebar))
   #+END_SRC

** crux
   #+BEGIN_SRC emacs-lisp
     (use-package crux
       :ensure t
       :bind (("C-a" . crux-move-beginning-of-line)
              ("C-c d" . crux-duplicate-current-line-or-region)
              ("C-c D" . crux-delete-file-and-buffer)
              ("C-c e" . crux-eval-and-replace)
              ("C-c I" . crux-find-user-init-file)
              ("C-c o o" . crux-open-with)
              ("C-c o r" . crux-sudo-edit)
              ("C-c r n" . crux-rename-file-and-buffer)
              ("C-c TAB" . crux-indent-defun)
              ("C-x K" . crux-kill-other-buffers)
              ("C-^" . crux-top-join-line)
              ("C-<BACKSPACE>" . crux-kill-line-backwards)
              ("C-S-<BACKSPACE>" . crux-kill-whole-line)))
   #+END_SRC

** key map
*** which-key
    #+BEGIN_SRC emacs-lisp
      (use-package which-key
        :ensure t
        :defer 10
        :diminish which-key-mode
        :config
        (which-key-mode 1))
    #+END_SRC

*** discover-my-major
    #+BEGIN_SRC emacs-lisp
      (use-package discover-my-major
        :ensure t
        :commands (discover-my-major discover-my-mode)
        :bind ("C-h m" . discover-my-major))
    #+END_SRC

** undo-tree
   #+BEGIN_SRC emacs-lisp
     (use-package undo-tree
       :ensure t
       :diminish undo-tree-mode
       :bind ("C-x u" . undo-tree-visualize)
       :config
       (global-undo-tree-mode t))
   #+END_SRC

** move cursor
*** avy
    jump to visible text using a char-based decision tree
    #+BEGIN_SRC emacs-lisp
      (use-package avy
        :ensure t
        :bind (("C-c j" . avy-goto-char-timer)
               ("M-g g" . avy-goto-line))
        :config
        (setq avy-background t))
    #+END_SRC

*** ace-window
    select a window
    #+BEGIN_SRC emacs-lisp
      (use-package ace-window
        :ensure t
        :bind ("C-x o" . ace-window))
    #+END_SRC

** multiple cursors
   #+begin_SRC emacs-lisp
     (use-package multiple-cursors
       :ensure t
       :bind (("C-|" . mc/edit-lines)
              ("C->" . mc/mark-next-like-this)
              ("C-<" . mc/mark-previous-like-this)
              ("C-S-<mouse-1>" . mc/add-cursor-on-click)))
   #+end_SRC

** search
   anzu-mode enhances isearch & query-replace by showing total matches and
   current match position in the mode-line
   #+BEGIN_SRC emacs-lisp
     (use-package anzu
       :ensure t
       :diminish anzu-mode
       :bind (("M-%" . anzu-query-replace)
              ("C-M-%" . anzu-query-replace-regexp))
       :config
       (global-anzu-mode t))
   #+END_SRC

** alert
   #+begin_src emacs-lisp
     (use-package alert
       :ensure nil
       :config
       (setq alert-default-style 'libnotify))
   #+end_src

** version control
*** Git
    magit
    #+BEGIN_SRC emacs-lisp
      (use-package magit
        :ensure t
        :mode ("/\\(\
      \\(\\(COMMIT\\|NOTES\\|PULLREQ\\|TAG\\)_EDIT\\|MERGE_\\|\\)MSG\
      \\|\\(BRANCH\\|EDIT\\)_DESCRIPTION\\)\\'" . git-commit-mode)
        :bind ("C-x g" . magit-status))
    #+END_SRC

    git modes
    #+BEGIN_SRC emacs-lisp
      (use-package gitconfig-mode
        :ensure t
        :mode ("/\\.gitconfig\\'" "/\\.git/config\\'" "/git/config\\'"
               "/\\.gitmodules\\'"))

      (use-package gitignore-mode
        :ensure t
        :mode ("/\\.gitignore\\'" "/\\.git/info/exclude\\'" "/git/ignore\\'"))
    #+END_SRC

*** diff-hl
    #+BEGIN_SRC emacs-lisp
      (use-package diff-hl
        :ensure t
        :defer 10
        :config
        (global-diff-hl-mode t)
        (add-hook 'dired-mode-hook 'diff-hl-dired-mode)
        (add-hook 'magit-post-refresh-hook 'diff-hl-magit-post-refresh))
    #+END_SRC

** completion
   #+BEGIN_SRC emacs-lisp
     (use-package company
       :ensure t
       :defer 10
       :diminish company-mode
       :config
       (global-company-mode 1))
   #+END_SRC

** spell checking
   flyspell
   #+BEGIN_SRC emacs-lisp
     (use-package flyspell
       :ensure t
       :defer 10
       :diminish flyspell-mode
       :preface
       (defvar my-enable-flyspell nil)
       (cond
        ((executable-find "aspell")
         (setq ispell-program-name "aspell")
         (setq my-enable-flyspell t))
        ((executable-find "hunspell")
         (setq ispell-program-name "hunspell")
         (setq ispell-dictionary "en_US")
         (setq my-enable-flyspell t))
        (t
         (message "Neither aspell nor hunspell found")))
       :if my-enable-flyspell
       :hook ((text-mode . flyspell-mode)
              (prog-mode . flyspell-prog-mode)))
   #+END_SRC

* Programming
** parenthesis
   smart parens
   #+BEGIN_SRC emacs-lisp
     (use-package smartparens
       :ensure t
       :defer 10
       :diminish smartparens-mode
       :hook (prog-mode . smartparens-strict-mode)
       :config
       (require 'smartparens-config)
       (show-smartparens-global-mode 1))
   #+END_SRC

   colorful parens
   #+BEGIN_SRC emacs-lisp
     (use-package rainbow-delimiters
       :ensure t
       :hook (prog-mode . rainbow-delimiters-mode))
   #+END_SRC

** flycheck
   #+BEGIN_SRC emacs-lisp
     (use-package flycheck
       :ensure t
       :diminish flycheck-mode
       :hook (prog-mode . flycheck-mode)
       :config
       (setq flycheck-display-errors-function
             'flycheck-display-error-messages-unless-error-list))
   #+END_SRC

** language server protocol
   lsp-mode
   #+BEGIN_SRC emacs-lisp
     (use-package lsp-mode
       :ensure t
       :commands (lsp lsp-mode))
   #+END_SRC

   lsp-ui
   #+BEGIN_SRC emacs-lisp
     (use-package lsp-ui
       :ensure t
       :after (lsp-mode)
       :commands (lsp-ui-mode))
   #+END_SRC

   company-lsp
   #+BEGIN_SRC emacs-lisp
     (use-package company-lsp
       :ensure t
       :after (company lsp-mode)
       :commands (company-lsp))
   #+END_SRC

   debug
   #+begin_src emacs-lisp
     (use-package dap-mode
       :ensure t
       :after (lsp-mode)
       :commands (dap-mode dap-ui-mode))
   #+end_src

** yasnippet
   #+begin_SRC emacs-lisp
     (use-package yasnippet
       :ensure t
       :diminish yas-minor-mode
       :config
       (add-to-list 'yas-snippet-dirs "~/Projects/guix/etc/snippets")
       (yas-global-mode 1))
   #+end_SRC

** Lisp
   indentation
   #+BEGIN_SRC emacs-lisp
     (use-package aggressive-indent
       :ensure t
       :diminish aggressive-indent-mode
       :hook ((emacs-lisp-mode scheme-mode) . aggressive-indent-mode))
   #+END_SRC

** Scheme
   geiser
   #+BEGIN_SRC emacs-lisp
     (use-package geiser
       :ensure t
       :hook (scheme-mode . geiser-mode--maybe-activate)
       :config
       (setq geiser-active-implementations '(guile))
       (setq geiser-mode-start-repl-p t)
       (setq geiser-repl-history-filename
             (expand-file-name "geiser_history" user-emacs-data-dir)))
   #+END_SRC

   guix
   #+BEGIN_SRC emacs-lisp
     (use-package guix
       :ensure t
       ;; :hook (scheme-mode . guix-devel-mode)
       )
   #+END_SRC

** Python
   Prefer Python 3
   #+BEGIN_SRC emacs-lisp
     (setq python-shell-interpreter "python3")
   #+END_SRC

   python mode
   #+BEGIN_SRC emacs-lisp
     (defun my-python-mode-setup ()
       (add-hook 'post-self-insert-hook
                 'electric-layout-post-self-insert-function
                 nil t))

     (add-hook 'python-mode-hook 'my-python-mode-setup)
   #+END_SRC

   anaconda-mode
   #+BEGIN_SRC emacs-lisp :tangle no
     (use-package anaconda-mode
       :ensure t
       :hook ((python-mode . anaconda-mode)
              (python-mode . anaconda-eldoc-mode)))

     (use-package company-anaconda
       :ensure t
       :after (company anaconda-mode)
       :config
       (push 'company-anaconda company-backends))
   #+END_SRC

   Language Server Protocol
   #+BEGIN_SRC emacs-lisp
     (add-hook 'python-mode-hook 'lsp)
   #+END_SRC

   Install python language server
   #+BEGIN_SRC sh
     pip3 install python-language-server[all]
   #+END_SRC

** Go
   #+BEGIN_SRC emacs-lisp
     (use-package go-mode
       :ensure t
       :mode ("\\.go\\'" . go-mode))

     (use-package go-eldoc
       :ensure t
       :after (go-mode)
       :hook (go-mode . go-eldoc-setup))
   #+END_SRC

   #+BEGIN_SRC emacs-lisp
       (defun my-go-mode-setup ()
         (add-hook 'before-save-hook 'gofmt-before-save nil t))

       (add-hook 'go-mode-hook 'my-go-mode-setup)
   #+END_SRC

   Language Server Protocol
   #+begin_SRC emacs-lisp
     (add-hook 'go-mode-hook 'lsp)
   #+end_SRC

   Install go language server
   #+BEGIN_SRC sh
     # See https://github.com/saibing/bingo/wiki/Install
   #+END_SRC

** C
   #+BEGIN_SRC emacs-lisp
     (setq c-default-style "linux")
     (setq-default c-basic-offset 4)
   #+END_SRC

   Language Server Protocol
   #+begin_SRC emacs-lisp
     (add-hook 'c-mode-hook 'lsp)
     (add-hook 'c++-mode-hook 'lsp)
   #+end_SRC

   Install C language server
   #+BEGIN_SRC sh
     # Install clangd.
   #+END_SRC

   clang-format
   #+BEGIN_SRC emacs-lisp
     (use-package clang-format
       :ensure t
       :commands (clang-format-buffer))

     (defun clang-format-buffer-smart ()
       "Reformat buffer if .clang-format exists in the projectile root"
       (when (f-exists? (expand-file-name ".clang-format" (projectile-project-root)))
         (clang-format-buffer)))

     (defun my-c-mode-setup ()
       (add-hook 'before-save-hook 'clang-format-buffer-smart nil t))

     (add-hook 'c-mode-hook 'my-c-mode-setup)
     (add-hook 'c++-mode-hook 'my-c-mode-setup)
   #+END_SRC

** Java
   Language Server Protocol
   #+BEGIN_SRC emacs-lisp
     (use-package lsp-java
       :ensure nil
       :after lsp-mode)
   #+END_SRC

** C#
   csharp-mode
   #+BEGIN_SRC emacs-lisp
     (use-package csharp-mode
       :ensure nil
       :mode ("\\.cs\\'" . csharp-mode)
       :config
       (defun my-csharp-mode-setup ()
         (c-set-style "c#"))

       (add-hook 'csharp-mode-hook 'my-csharp-mode-setup))
   #+END_SRC

   OmniSharp
   #+BEGIN_SRC emacs-lisp
     (use-package omnisharp
       :ensure nil
       :hook (csharp-mode . omnisharp-mode))
   #+END_SRC

   Install OmniSharp server: ~M-x omnisharp-install-server~

** fish shell
   #+BEGIN_SRC emacs-lisp
     (use-package fish-mode
       :ensure t
       :mode ("\\.fish\\'" . fish-mode)
       :interpreter ("fish"))
   #+END_SRC

* Markup Languages
** Org Mode
*** org
    #+BEGIN_SRC emacs-lisp
      (use-package org
        :ensure org-plus-contrib
        :defer 10
        :mode ("\\.org\\'" . org-mode)
        :bind (("C-c C-," . org-insert-structure-template)
               ("C-c c" . org-capture)
               ("C-c l" . org-store-link)
               ("C-c a" . org-agenda))
        :config
        (setq org-directory "~/Sync/Org")
        (setq org-agenda-files (list org-directory)
              org-agenda-file-regexp "\\`[^.].*\\.org\\'")
        (setq org-default-notes-file
              (expand-file-name "Organizer.org" org-directory))
        ;; Align tags' right edge with 80th column
        (setq org-tags-column -80)
        (setq org-agenda-default-appointment-duration 60)
        (setq org-log-done t)
        (setq org-catch-invisible-edits 'show)
        (setq org-fast-tag-selection-single-key 'expert)
        ;; Do not store org id
        (setq org-id-track-globally nil)
        (setq org-support-shift-select t)
        (setq org-use-sub-superscripts '{})

        (setq my-org-inbox-file (expand-file-name "Inbox.org" org-directory))

        ;;; org-capture
        (setq org-capture-templates
              `(("t" "todo"
                 entry (file+olp org-default-notes-file "Agenda")
                 "* TODO %?\n  :PROPERTIES:\n  :Captured_at: %U\n  :END:\n")
                ("j" "journal"
                 entry (file+olp org-default-notes-file "Journal")
                 "* %u\n%?\n")))

        (add-to-list 'org-structure-template-alist '("se" . "src emacs-lisp"))
        (add-to-list 'org-structure-template-alist '("ss" . "src sh"))


        ;;; org-babel
        (org-babel-do-load-languages
         'org-babel-load-languages
         '((dot . t)
           (latex . t)
           (ledger . t)
           (shell . t)
           (python . t)))

        ;; prefer python3
        (setq org-babel-python-command "python3")

        ;; disable emacs-lisp-checker for org-src-mode
        (add-hook 'org-src-mode-hook
                  (lambda ()
                    (setq-local flycheck-disabled-checkers
                                '(emacs-lisp-checkdoc))))


        ;;; org-export
        (setq org-export-with-toc nil)
        (setq org-export-with-section-numbers nil)
        (setq org-export-with-sub-superscripts '{})
        (setq org-export-exclude-tags '("noexport" "private"))

        ;;; org-html
        (setq org-html-validation-link nil)
        (setq org-html-doctype "html5")
        (setq org-html-html5-fancy-p t)

        ;;; org-latex
        (add-to-list 'org-latex-packages-alist '("" "listings"))
        (add-to-list 'org-latex-packages-alist '("" "color"))
        (setq org-latex-listings t
              org-latex-listings-options '(("basicstyle" "\\small")
                                           ("frame" "single")))


        ;;; org-icalendar
        ;; Include tasks that are not in DONE state.
        (setq org-icalendar-include-todo t)
        ;; Whether to make events from plain time stamps.
        (setq org-icalendar-with-timestamps 'active)
        ;; Include scheduled and deadline events.
        (setq org-icalendar-use-scheduled
              '(event-if-todo event-if-not-todo todo-start))
        (setq org-icalendar-use-deadline
              '(event-if-todo event-if-not-todo todo-due))
        ;; Trigger alarm 60 minutes before the event.
        (setq org-icalendar-alarm-time 60)
        (setq org-icalendar-exclude-tags
              '("noexport" "private" "archive" "journal"))
        (setq org-icalendar-combined-agenda-file
              (concat org-directory "/agenda.ics"))


        ;;; the following section is stolen from Purcell

        ;;; To-do settings

        (setq org-todo-keywords
              (quote ((sequence "TODO(t)" "NEXT(n)" "|" "DONE(d!/!)" "CANCELLED(c@/!)")
                      (sequence "PROJECT(p)" "|" "DONE(d!/!)" "CANCELLED(c@/!)")
                      (sequence "WAITING(w@/!)" "DELEGATED(e!)" "HOLD(h)" "|" "CANCELLED(c@/!)")))
              org-todo-repeat-to-state "NEXT")

        (setq org-todo-keyword-faces
              (quote (("NEXT" :inherit warning)
                      ("PROJECT" :inherit font-lock-string-face))))


        ;;; Agenda views

        (setq-default org-agenda-clockreport-parameter-plist '(:link t :maxlevel 3))


        (let ((active-project-match "-INBOX/PROJECT"))

          (setq org-stuck-projects
                `(,active-project-match ("NEXT")))

          (setq org-agenda-compact-blocks t
                org-agenda-sticky t
                org-agenda-start-on-weekday nil
                org-agenda-span 'day
                org-agenda-include-diary nil
                org-agenda-sorting-strategy
                '((agenda habit-down time-up user-defined-up effort-up category-keep)
                  (todo category-up effort-up)
                  (tags category-up effort-up)
                  (search category-up))
                org-agenda-window-setup 'current-window
                org-agenda-custom-commands
                `(("N" "Notes" tags "NOTE"
                   ((org-agenda-overriding-header "Notes")
                    (org-tags-match-list-sublevels t)))
                  ("g" "GTD"
                   ((agenda "" nil)
                    (tags "INBOX"
                          ((org-agenda-overriding-header "Inbox")
                           (org-tags-match-list-sublevels nil)))
                    (stuck ""
                           ((org-agenda-overriding-header "Stuck Projects")
                            (org-agenda-tags-todo-honor-ignore-options t)
                            (org-tags-match-list-sublevels t)
                            (org-agenda-todo-ignore-scheduled 'future)))
                    (tags-todo "-INBOX"
                               ((org-agenda-overriding-header "Next Actions")
                                (org-agenda-tags-todo-honor-ignore-options t)
                                (org-agenda-todo-ignore-scheduled 'future)
                                (org-agenda-skip-function
                                 '(lambda ()
                                    (or (org-agenda-skip-subtree-if 'todo '("HOLD" "WAITING"))
                                        (org-agenda-skip-entry-if 'nottodo '("NEXT")))))
                                (org-tags-match-list-sublevels t)
                                (org-agenda-sorting-strategy
                                 '(todo-state-down effort-up category-keep))))
                    (tags-todo ,active-project-match
                               ((org-agenda-overriding-header "Projects")
                                (org-tags-match-list-sublevels t)
                                (org-agenda-sorting-strategy
                                 '(category-keep))))
                    (tags-todo "-INBOX/-NEXT"
                               ((org-agenda-overriding-header "Orphaned Tasks")
                                (org-agenda-tags-todo-honor-ignore-options t)
                                (org-agenda-todo-ignore-scheduled 'future)
                                (org-agenda-skip-function
                                 '(lambda ()
                                    (or (org-agenda-skip-subtree-if 'todo '("PROJECT" "HOLD" "WAITING" "DELEGATED"))
                                        (org-agenda-skip-subtree-if 'nottododo '("TODO")))))
                                (org-tags-match-list-sublevels t)
                                (org-agenda-sorting-strategy
                                 '(category-keep))))
                    (tags-todo "/WAITING"
                               ((org-agenda-overriding-header "Waiting")
                                (org-agenda-tags-todo-honor-ignore-options t)
                                (org-agenda-todo-ignore-scheduled 'future)
                                (org-agenda-sorting-strategy
                                 '(category-keep))))
                    (tags-todo "/DELEGATED"
                               ((org-agenda-overriding-header "Delegated")
                                (org-agenda-tags-todo-honor-ignore-options t)
                                (org-agenda-todo-ignore-scheduled 'future)
                                (org-agenda-sorting-strategy
                                 '(category-keep))))
                    (tags-todo "-INBOX"
                               ((org-agenda-overriding-header "On Hold")
                                (org-agenda-skip-function
                                 '(lambda ()
                                    (or (org-agenda-skip-subtree-if 'todo '("WAITING"))
                                        (org-agenda-skip-entry-if 'nottodo '("HOLD")))))
                                (org-tags-match-list-sublevels nil)
                                (org-agenda-sorting-strategy
                                 '(category-keep))))
                    ;; (tags-todo "-NEXT"
                    ;;            ((org-agenda-overriding-header "All other TODOs")
                    ;;             (org-match-list-sublevels t)))
                    )))))


        (add-hook 'org-agenda-mode-hook 'hl-line-mode)


        ;;; Org clock

        ;; Save the running clock and all clock history when exiting Emacs, load it on startup
        (org-clock-persistence-insinuate)
        (setq org-clock-persist t)
        (setq org-clock-in-resume t)

        ;; Save clock data and notes in the LOGBOOK drawer
        (setq org-clock-into-drawer t)
        ;; Save state changes in the LOGBOOK drawer
        (setq org-log-into-drawer t)
        ;; Removes clocked tasks with 0:00 duration
        (setq org-clock-out-remove-zero-time-clocks t)

        ;; Show clock sums as hours and minutes, not "n days" etc.
        (setq org-time-clocksum-format
              '(:hours "%d" :require-hours t :minutes ":%02d" :require-minutes t))
        )
    #+END_SRC

*** org-alert
    Notifications for org agenda items
    #+BEGIN_SRC emacs-lisp
      (use-package org-alert
        :ensure t
        :defer 20
        :config
        (setq org-alert-interval 600)
        (org-alert-enable))
    #+END_SRC

*** ox-hugo
    #+BEGIN_SRC emacs-lisp
      (use-package ox-hugo
        :ensure t
        :after ox)
    #+END_SRC

*** Add UUID to all org headlines
    #+BEGIN_SRC emacs-lisp
      (defun my-add-uuid-to-org-headlines-in-buffer ()
        "Add ID properties to all headlines in the current buffer."
        (interactive)
        (org-map-entries 'org-id-get-create))
    #+END_SRC

** HTML
   htmlize -- Convert buffer text and decorations to HTML
   #+BEGIN_SRC emacs-lisp
     (use-package htmlize
       :ensure t
       :defer 10)
   #+END_SRC

   rainbow mode
   #+BEGIN_SRC emacs-lisp
     (use-package rainbow-mode
       :ensure t
       :hook ((html-mode css-mode) . rainbow-mode))
   #+END_SRC

** YAML
   #+BEGIN_SRC emacs-lisp
     (use-package yaml-mode
       :ensure t
       :mode ("\\.yaml\\'" "\\.yml\\'"))
   #+END_SRC

** CSV
   #+BEGIN_SRC emacs-lisp
     (use-package csv-mode
       :ensure t
       :mode ("\\.csv\\'" . csv-mode))
   #+END_SRC

** Ledger
   #+BEGIN_SRC emacs-lisp
     (use-package ledger-mode
       :ensure nil
       :mode ("\\.ledger\\'" . ledger-mode)
       :config
       (use-package flycheck-ledger
         :ensure t))
   #+END_SRC

** TeX
   auctex
   #+BEGIN_SRC emacs-lisp
     (use-package auctex
       :ensure nil
       :mode ("\\.tex\\'" . latex-mode)
       :config
       (setq TeX-auto-save t
             TeX-parse-self t
             TeX-PDF-mode t)
       (setq-default TeX-master nil)

       (defun my-latex-mode-setup ()
         (LaTeX-preview-setup)
         (LaTeX-math-mode))

       (add-hook 'latex-mode-hook 'my-latex-mode-setup)

       (use-package company-auctex
         :ensure t
         :after (company auctex)
         :hook (latex-mode . company-auctex-init))

       (use-package reftex
         :ensure t
         :hook (latex-mode . turn-on-reftex)
         :config
         (setq reftex-plug-into-AUCTeX t))
       )
   #+END_SRC

** Markdown
   #+BEGIN_SRC emacs-lisp
     (use-package markdown-mode
       :ensure nil
       :mode (("\\.md\\'" . markdown-mode)
              ("\\.markdown\\'" . markdown-mode)
              ("README\\.md\\'" . gfm-mode)))
   #+END_SRC

** restclient
   #+BEGIN_SRC emacs-lisp
     (use-package restclient
       :ensure t
       :mode ("\\.restclient\\'" . restclient-mode))
   #+END_SRC

   org-babel restclient
   #+BEGIN_SRC emacs-lisp
     (use-package ob-restclient
       :ensure t
       :after (ob restclient))
   #+END_SRC

* Internet
** Gnus
   #+BEGIN_SRC emacs-lisp
     (use-package gnus
       :commands (gnus)
       :config
       ;; Email servers
       (setq my--gnus-local '(nnmaildir "local"
                                        (directory "~/Mail")
                                        (get-new-mail nil)))
       ;; Usenet servers
       (setq my--gnus-gmane
             '(nntp "gmane"
                    (nntp-address "news.gmane.org")
                    (nntp-port-number 563)
                    (nntp-open-connection-function nntp-open-tls-stream))
             my--gnus-aioe
             '(nntp "aioe"
                    (nntp-address "nntp.aioe.org")
                    (nntp-port-number 563)
                    (nntp-open-connection-function nntp-open-tls-stream)))

       (setq gnus-select-method my--gnus-local)
       ;; (setq gnus-secondary-select-methods
       ;;       (list my--gnus-gmane my--gnus-aioe))
       )
   #+END_SRC

** mu4e
   #+begin_SRC emacs-lisp
     (setq mail-user-agent 'mu4e-user-agent)

     (use-package mu4e
       :ensure nil
       :bind (("C-c m" . mu4e))
       :config

       (setq mu4e-confirm-quit nil)

       ;; Don't save message to Sent folder if IMAP takes care of this.
       ;; (setq mu4e-sent-messages-behavior 'delete)

       ;; Fetch email.
       (setq mu4e-get-mail-command "offlineimap")

       ;; Default context.
       (setq mu4e-maildir "~/Mail")
       (setq mu4e-drafts-folder "/drafts")
       (setq mu4e-refile-folder "/archive")
       (setq mu4e-sent-folder   "/sent")
       (setq mu4e-trash-folder  "/trash")

       ;; ;; Contexts
       ;; (setq mu4e-contexts
       ;;       (list
       ;;        (make-mu4e-context
       ;;         :name "gmail"
       ;;         :match-func
       ;;         (lambda (msg)
       ;;           (when msg
       ;;             (string-prefix-p "/Gmail"
       ;;                              (mu4e-message-field msg :maildir))))
       ;;         :vars
       ;;         `((mu4e-drafts-folder . "/Gmail/[Gmail].Drafts")
       ;;           (mu4e-sent-folder   . "/Gmail/[Gmail].Sent Mail")
       ;;           (mu4e-trash-folder  . "/Gmail/[Gmail].Trash")
       ;;           (mu4e-refile-folder . "/Gmail/[Gmail].All Mail")
       ;;           (user-mail-address  . ,my-gmail-address)
       ;;           (message-signature-file . "personal")))))

       ;; Setup some handy shortcuts.
       ;; Jump to Inbox by pressing `ji'.
       ;; Move email to Archive folder by pressing `ma'.
       (setq mu4e-maildir-shortcuts
             '(("/archive" . ?a)
               ("/drafts"  . ?d)
               ("/INBOX"   . ?i)
               ("/sent"    . ?s)
               ("/spam"    . ?j)
               ("/trash"   . ?t))))
   #+end_SRC

** IRC
   ERC
   #+BEGIN_SRC emacs-lisp
     (use-package erc
       :commands (erc my-erc-start-or-switch)
       :config
       (setq erc-nick "meiyopeng")
       (setq erc-autojoin-channels-alist
             '((".*\\.freenode.net" "#emacs")))
       (erc-autojoin-mode t)

       ;; spell checking
       (erc-spelling-mode 1)

       ;; logging
       (setq erc-log-channels-directory
             (expand-file-name "erc" (xdg-data-home)))

       (setq erc-save-buffer-on-part t)

       ;; fallback to auth-source
       (setq erc-prompt-for-password nil)

       ;; Kill buffers for channels after /part
       (setq erc-kill-buffer-on-part t)
       ;; Kill buffers for private queries after quitting the server
       (setq erc-kill-queries-on-quit t)
       ;; Kill buffers for server messages after quitting the server
       (setq erc-kill-server-buffer-on-quit t)

       ;; open query buffers in the current window
       (setq erc-query-display 'buffer)

       (setq erc-auto-reconnect nil)

       (erc-track-mode t)
       (setq erc-track-exclude-types '("JOIN" "NICK" "PART" "QUIT" "MODE"
                                       "324" "329" "332" "333" "353" "477"))

       (defun my-erc-start-or-switch ()
         "Connect to ERC, or switch to last active buffer"
         (interactive)
         (if (get-buffer "irc.freenode.net:6697")
             (erc-track-switch-buffer 1)
           (when (y-or-n-p "Start ERC? ")
             (erc-tls :server "irc.freenode.net" :port 6697
                      :nick my-irc-nick)))))
   #+END_SRC

** RSS feed
   elfeed
   #+BEGIN_SRC emacs-lisp
     (use-package elfeed
       :ensure t
       :bind (("C-c n" . my-elfeed-open))
       :config
       (setq elfeed-db-directory
             (expand-file-name "elfeed" (xdg-data-home)))

       (use-package elfeed-org
         :ensure t
         :after (elfeed)
         :config
         (setq rmh-elfeed-org-files
               (list (expand-file-name "elfeed.org" user-emacs-config-dir)))
         (elfeed-org))

       (defun my-elfeed-open ()
         "Wrapper to load the elfeed db from disk before opening"
         (interactive)
         (elfeed-db-load)
         (elfeed)))
   #+END_SRC

** lookup Wikipedia
   #+BEGIN_SRC emacs-lisp
     (require 'browse-url)

     (defun my-lookup-wikipedia ()
       "Look up the word under cursor in Wikipedia.
     If there is a text selection, use that."
       (interactive)
       (let (word)
         (setq word
               (if (use-region-p)
                   (buffer-substring-no-properties (region-beginning) (region-end))
                 (current-word)))
         (setq word (replace-regexp-in-string " " "_" word))
         (browse-url (concat "http://en.wikipedia.org/wiki/" word))))
   #+END_SRC

** lookup Wiktionary
   #+BEGIN_SRC emacs-lisp
     (autoload 'ispell-get-word "ispell")

     (defun my-lookup-wiktionary (word)
       (interactive (list (save-excursion (car (ispell-get-word nil)))))
       (browse-url (format "http://en.wiktionary.org/wiki/%s" word)))

     (global-set-key (kbd "M-#") 'my-lookup-wiktionary)
   #+END_SRC

* Media
** pdf-tools
   #+BEGIN_SRC emacs-lisp
     (use-package pdf-tools
       :ensure nil
       :mode ("\\.pdf\\'" . pdf-view-mode)
       :config
       (pdf-loader-install))
   #+END_SRC

* Developer Tools
** debbugs
   #+begin_src emacs-lisp
     (use-package debbugs
       :ensure nil
       :commands (debbugs-gnu
                  debbugs-org
                  debbugs-gnu-bugs
                  debbugs-org-bugs
                  debbugs-gnu-search
                  debbugs-org-search)
       :config
       (setq debbugs-gnu-default-packages '("guix")))
   #+end_src

* Chinese compatibility hack
** Chinese Font
   To align Chinese characters with English characters vertically, the Chinese
   font size should be rescaled.
   #+BEGIN_EXAMPLE
     chinese_font_size = 1.2 * english_font_zise
     chinese_character_width = 2 * english_character_width
   #+END_EXAMPLE

   #+BEGIN_SRC emacs-lisp
     (defun my-set-chinese-font (font-list &optional font-size)
       "Set Chinese font to the first available font in FONT-LIST."
       (let ((chinese-font (cl-find-if #'my-font-available-p font-list)))

         (if (null chinese-font)
             (user-error "No font is available in FONT-LIST"))

         (message "Set Chinese font to %s" chinese-font)
         (dolist (charset '(han cjk-misc))
           (set-fontset-font t charset
                             (font-spec :family chinese-font :size font-size)))

         ;; Rescale Chinese fonts
         (setq face-font-rescale-alist
               '((".*WenQuanYi.*" . 1.2)
                 (".*Heiti.*" . 1.2)
                 (".*Yahei.*" . 1.2)))
         ))
   #+END_SRC

   My preferred fonts
   #+BEGIN_SRC emacs-lisp
     (when (display-graphic-p)
       (my-set-chinese-font
        '("WenQuanYi Micro Hei" "WenQuanYi Zen Hei" "Heiti SC" "STHeiti" "Microsoft Yahei")))
   #+END_SRC

** Fix line break in Chinese paragraph
   by zwz.github.io
   #+BEGIN_SRC emacs-lisp
     (defun clear-single-linebreak-in-cjk-string (string)
       "clear single line-break between cjk characters that is usually soft
     line-breaks"
       (let* ((cjk-char "[\u3000-\u303F]\\|[\u4E00-\u9FFF]\\|[\uFF01-\uFF5E]")
              (regexp (concat "\\(" cjk-char "\\)\n\\(" cjk-char "\\)"))
              (start (string-match regexp string)))
         (while start
           (setq string (replace-match "\\1\\2" nil nil string)
                 start (string-match regexp string start))))
       string)

     (defun ox-html-clear-single-linebreak-for-cjk (string backend info)
       (when (org-export-derived-backend-p backend 'html)
         (clear-single-linebreak-in-cjk-string string)))

     (eval-after-load "ox"
       '(add-to-list 'org-export-filter-final-output-functions
                     'ox-html-clear-single-linebreak-for-cjk))
   #+END_SRC

* init-local.el
  Load an optional local init file
  #+BEGIN_SRC emacs-lisp
    (load (expand-file-name "init-local.el" user-emacs-config-dir) 'noerror)
  #+END_SRC
